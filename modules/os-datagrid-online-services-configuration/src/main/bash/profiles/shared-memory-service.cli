embed-server --server-config=services.xml --std-out=echo

if (outcome == success) of /subsystem=datagrid-infinispan/cache-container=clustered/configurations=CONFIGURATIONS/distributed-cache-configuration=caching-service:read-resource
   /subsystem=datagrid-infinispan/cache-container=clustered/configurations=CONFIGURATIONS/distributed-cache-configuration=caching-service:remove
end-if

if (result != $num_owners) of /subsystem=datagrid-infinispan/cache-container=clustered/configurations=CONFIGURATIONS/distributed-cache-configuration=shared-memory-service:read-attribute(name=owners)
   /subsystem=datagrid-infinispan/cache-container=clustered/configurations=CONFIGURATIONS/distributed-cache-configuration=shared-memory-service:write-attribute(name=owners, value=$num_owners)
end-if

if (outcome == success) of /subsystem=datagrid-infinispan-endpoint/rest-connector=rest-connector/authentication=AUTHENTICATION:read-resource
   /subsystem=datagrid-infinispan-endpoint/rest-connector=rest-connector/authentication=AUTHENTICATION:remove
end-if

# Write auth values. This should be in services.xml eventually as is static
/subsystem=datagrid-infinispan-endpoint/rest-connector=rest-connector/authentication=AUTHENTICATION:add(auth-method=BASIC, security-realm=ApplicationRealm)
/subsystem=datagrid-infinispan-endpoint/hotrod-connector=hotrod-connector/authentication=AUTHENTICATION:add(security-realm=ApplicationRealm)
/subsystem=datagrid-infinispan-endpoint/hotrod-connector=hotrod-connector/authentication=AUTHENTICATION/sasl=SASL:add(server-name=shared-memory-service, mechanisms=[DIGEST-MD5], qop=[auth])

# Remove memcached as it is not secured
if (outcome == success) of /subsystem=datagrid-infinispan/cache-container=clustered/distributed-cache=memcachedCache:read-resource
   /subsystem=datagrid-infinispan/cache-container=clustered/distributed-cache=memcachedCache:remove
end-if

if (outcome == success) of /subsystem=datagrid-infinispan-endpoint/memcached-connector=memcached-connector:read-resource
   /subsystem=datagrid-infinispan-endpoint/memcached-connector=memcached-connector:remove
end-if

/subsystem=datagrid-infinispan/cache-container=clustered/distributed-cache=default:write-attribute(name=configuration, value=shared-memory-service)

# This is a temporary solution until we find out what the proper way is. Alternatively this could be implemented in
# services.xml however we are not fully committed to OpenShift PING at this point.
if (outcome == success) of /subsystem=datagrid-jgroups/stack=kubernetes:read-resource
   /subsystem=datagrid-jgroups/stack=kubernetes:remove
   /subsystem=datagrid-jgroups/stack=kubernetes:add(transport={type=TCP, socket-binding=jgroups-tcp}, protocols=[{type=openshift.KUBE_PING, socket-binding=jgroups-mping},{type=MERGE3},{type=FD_SOCK,socket-binding=jgroups-tcp-fd},{type=FD_ALL},{type=VERIFY_SUSPECT},{type=pbcast.NAKACK2},{type=UNICAST3},{type=pbcast.STABLE},{type=pbcast.GMS},{type=MFC},{type=FRAG3}])
   /subsystem=datagrid-jgroups/stack=kubernetes/protocol=pbcast.NAKACK2/property=use_mcast_xmit:add(value=false)
end-if

# Below settings were introduced to tune performance. Once we confirm them, we will add some tests.
/subsystem=datagrid-infinispan/cache-container=clustered/thread-pool=transport:write-attribute(name=min-threads, value=1)
/subsystem=datagrid-infinispan/cache-container=clustered/thread-pool=async-operations:write-attribute(name=min-threads, value=1)
/subsystem=io/worker=default:write-attribute(name=io-threads, value=0)
/subsystem=io/worker=default:write-attribute(name=task-max-threads, value=0)

stop-embedded-server
